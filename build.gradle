group 'com.fredun'
version '0.0.1-SNAPSHOT'

buildscript {
	ext {
		versionKotlin = '1.0.1-2'
		versionAntlr = '4.5.3'
		versionJUnit = '4.12'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versionKotlin"
	}
}

repositories {
	mavenCentral()
}

apply plugin: 'kotlin'
apply plugin: 'antlr'
apply plugin: 'application'
apply plugin: 'maven-publish'

mainClassName = 'com.fredun.frontend.MainKt'

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib:$versionKotlin"
	compile "org.jetbrains.kotlin:kotlin-reflect:$versionKotlin"

	antlr "org.antlr:antlr4:$versionAntlr"
	compile "org.antlr:antlr4-runtime:$versionAntlr"
	testCompile "junit:junit:$versionJUnit"
}

compileKotlin.dependsOn generateGrammarSource
compileTestKotlin.dependsOn generateTestGrammarSource

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifact sourceJar {
				classifier "sources"
			}
		}
	}
}
